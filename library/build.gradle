apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

android {
    namespace = "com.yqritc.recyclerviewflexibledivider"

    compileSdkVersion 30
    //buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 15
        versionName "1.4.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lint {
        checkReleaseBuilds = false
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
}

android.libraryVariants.all { variant ->
    if (variant.buildType.isDebuggable()) {
        return; // Skip debug builds.
    }
    task("javadoc${variant.name.capitalize()}", type: Javadoc) {
        description "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = System.getenv("ANDROID_HOME") + "/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
    }

    task("bundleJavadoc${variant.name.capitalize()}", type: Jar) {
        description "Bundles Javadoc into zip for $variant.name."
        archiveClassifier = "javadoc"
        from tasks["javadoc${variant.name.capitalize()}"]
    }
}

apply from: 'android-artifacts.gradle'
apply from: 'bintray-publish.gradle'
